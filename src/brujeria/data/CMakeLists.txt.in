cmake_minimum_required(VERSION 3.18)
cmake_policy(VERSION 3.18)
project(${BRUJERIA_PROJECT_NAME}
  DESCRIPTION "${BRUJERIA_PROJECT_DESCRIPTION}"
  VERSION "${BRUJERIA_PROJECT_VERSION}")

find_package(Python COMPONENTS Development.Module REQUIRED)

file(GLOB_RECURSE sources CONFIGURE_DEPENDS ${BRUJERIA_MODULE_PATH}/src/*)

string(CONCAT is-mingw $<AND:
  $<COMPILE_LANG_AND_ID:CXX,GNU>,
  $<PLATFORM_ID:Windows>
>)

add_library(${PROJECT_NAME} MODULE)
target_sources(${PROJECT_NAME} PRIVATE ${sources})

target_include_directories(${PROJECT_NAME}
  PRIVATE
    $<BUILD_INTERFACE:${BRUJERIA_MODULE_PATH}/include>)
target_compile_definitions(${PROJECT_NAME} PRIVATE  $<${is-mingw}:_hypot=hypot>)
target_compile_options(${PROJECT_NAME}
  PRIVATE
    $<$<COMPILE_LANG_AND_ID:CXX,MSVC>:/permissive->
    "$<${is-mingw}:-include cmath>"
    $<$<COMPILE_LANG_AND_ID:CXX,GNU>:-fdiagnostic-color=always>
    $<$<COMPILE_LANG_AND_ID:CXX,AppleClang,Clang>:-fcolor-diagnostics>)
target_link_libraries(${PROJECT_NAME} PRIVATE Python::Module)

set_property(TARGET ${PROJECT_NAME} PROPERTY SUFFIX ${Python_SOABI})
set_property(TARGET ${PROJECT_NAME} PROPERTY PREFIX)

include(${BRUJERIA_MODULE_PATH}/init.cmake)
